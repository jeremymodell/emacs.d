(require 'message)
(require 'notmuch)
(require 'notmuch-address)
(setq notmuch-address-command (expand-file-name "~/Mail/bin/search-addressbook.pl"))
(notmuch-address-message-insinuate)

(defun jph/mutt-fakeditor-exit-action ()
  (when (frame-parameter nil 'jph/mutt-fakeditor)
    (let ((message-file-path (buffer-file-name)))
      (kill-buffer)
      (delete-file message-file-path))
    (delete-frame)))

(defun jph/message-kill-buffer ()
  (interactive)
  (if (frame-parameter nil 'jph/mutt-fakeditor)
      (jph/mutt-fakeditor-exit-action)
    (message-kill-buffer)))

(define-key message-mode-map "\C-c\C-k" 'jph/message-kill-buffer)
(define-key message-mode-map "\C-ck" 'jph/message-kill-buffer)

(message-add-action
 '(jph/mutt-fakeditor-exit-action)
 'exit)

(defun jph/message/raise-if-fakeditor ()
  (when (frame-parameter nil 'jph/mutt-fakeditor)
    (raise-frame)))

(add-hook 'message-mode-hook 'jph/message/raise-if-fakeditor)

(defun jph/message-send-with-msmtp ()
  (let ((sendmail-program "msmtp")
        (message-sendmail-f-is-evil 't)
        (message-sendmail-extra-arguments '("--read-envelope-from" "-a" "pasternacki.net")))
    (message-send-mail-with-sendmail)))

(setq message-send-mail-function 'jph/message-send-with-msmtp)

(setq notmuch-archive-tags '("-inbox" "-unread"))

(defmacro jph/notmuch-add-tag-mapping (key &rest tags)
  `(progn
     (define-key notmuch-show-mode-map ,key
       (lambda ()
         (interactive)
         (notmuch-show-tag ',tags)
         (notmuch-show-next-message)))
     (define-key notmuch-search-mode-map ,key
       (lambda ()
         (interactive)
         (notmuch-search-tag ',tags)
         (notmuch-search-next-thread)))))

(jph/notmuch-add-tag-mapping "S" "-inbox" "-unread" "+spam")
(jph/notmuch-add-tag-mapping "d" "-inbox" "-unread" "+deleted")
(jph/notmuch-add-tag-mapping "D" "-inbox" "-unread" "+junk")
(jph/notmuch-add-tag-mapping "k" "-inbox" "-unread" "+killed")
(jph/notmuch-add-tag-mapping "u" "+inbox" "+unread" "-spam" "-junk" "-deleted" "-killed")

(defun jph/notmuch-ido-address-selection-function (prompt collection initial-input)
  (ido-completing-read
   prompt (cons initial-input collection) nil nil nil 'notmuch-address-history))

(setq notmuch-address-selection-function 'jph/notmuch-ido-address-selection-function)
